if [ -e /proc/version ] && grep -q Microsoft /proc/version; then
  # See https://github.com/microsoft/WSL/issues/352
  if [ "$(umask)" = '000' ]; then
    #if [ -e /etc/login.defs ] && grep -q '^[[:space:]]*USERGROUPS_ENAB[[:space:]]\{1,\}yes' /etc/login.defs; then
    #  umask 002
    #else
      umask 022
    #fi
  fi
fi

# shortcut to this dotfiles path is $ZSH
export DOTFILES=$HOME/.dotfiles

# your project folder that we can `c [tab]` to
export PROJECTS=~/workspace

# Stash your environment variables in ~/.localrc. This means they'll stay out
# of your main dotfiles repository (which may be public, like this one), but
# you'll have access to them in your scripts.
if [[ -a ~/.localrc ]]
then
  source ~/.localrc
fi

# all of our zsh files
typeset -U config_files_
config_files_=($DOTFILES/*/*.zsh)

# load the path files
for file in ${(M)config_files_:#*/path.zsh}
do
  source $file
done

# load everything but the path and completion files
for file in ${${config_files_:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit

# load every completion after autocomplete loads
for file in ${(M)config_files_:#*/completion.zsh}
do
  source $file
done

unset config_files_
